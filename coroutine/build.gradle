group 'com.gnefedev'
version '1.0'

buildscript {
    ext.kotlin_version = '1.2.0-beta-31'
    ext.springBootVersion = '1.5.7.RELEASE'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.2"
        }
        mavenLocal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.2"
    }
    mavenLocal()
}

apply plugin: 'application'
apply plugin: 'kotlin-kapt'
apply plugin: "kotlin-allopen"
apply plugin: "kotlin-spring"
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

idea {
    module {
        sourceDirs += file("src/main/kotlin")
    }
}

mainClassName = "com.gnefedev.coroutine.ApplicationKt"

allOpen {
    annotations(["org.openjdk.jmh.annotations.Fork", "org.openjdk.jmh.annotations.State"])
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

dependencies {
    compile 'org.openjdk.jmh:jmh-core:1.19'
    compile 'org.openjdk.jmh:jmh-generator-annprocess:1.19'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.18"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-nio:0.18"

    compile 'org.asynchttpclient:async-http-client:2.0.37'
    compile 'com.github.mauricio:postgresql-async_2.12:0.2.21'

    compile "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"

    compile 'postgresql:postgresql:9.4.1208-jdbc42-atlassian-hosted'

    compile 'org.slf4j:jcl-over-slf4j:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.1.11'
    compile 'io.github.microutils:kotlin-logging:1.4.1'
}

dependencies {
    kapt 'org.openjdk.jmh:jmh-core:1.19'
    kapt 'org.openjdk.jmh:jmh-generator-annprocess:1.19'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task buildDocker(type: Docker, dependsOn: build) {
    applicationName = jar.baseName
    dockerfile = file('Dockerfile')
    doFirst {
        copy {
            from jar
            into "$stageDir"
        }
    }
}
